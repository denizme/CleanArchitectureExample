ext.versions = [
        minSdk    : 21,
        compileSdk: 31,
        targetSdk : 31
]

android {
    compileSdk versions.compileSdk

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    testOptions.unitTests {
        includeAndroidResources = true
        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }
}

ext.applyAndroidXDependencies = { ->
    dependencies {
        api 'androidx.core:core-ktx:1.7.0'
        api 'androidx.appcompat:appcompat:1.4.1'

        def lifecycleVersion = "2.4.1"
        api "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
        api "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
        api "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

        api 'androidx.fragment:fragment-ktx:1.4.1'
    }
}


ext.applyNavigationDependencies = { ->
    dependencies {
        def navigationVersion = "2.4.1"
        api "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
        api "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    }
}


ext.applyHiltDependencies = { ->
    def hiltVersion = "2.38.1"
    dependencies {
        implementation "com.google.dagger:hilt-android:$hiltVersion"
        kapt "com.google.dagger:hilt-compiler:$hiltVersion"
    }
}

ext.applyTestDependencies = { ->
    dependencies {
        //test
        testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'
        testImplementation 'junit:junit:4.+'
        testImplementation 'io.mockk:mockk:1.12.0'
        testImplementation 'androidx.test.ext:truth:1.4.0'
        testImplementation 'androidx.arch.core:core-testing:2.1.0'
        testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'

        //androidTest
        androidTestImplementation "junit:junit:4.+"
        androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
        androidTestImplementation 'androidx.test.ext:junit:1.1.3'
        androidTestImplementation "com.google.truth:truth:1.0.1"
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
        androidTestImplementation 'com.android.support.test:rules:1.0.2'
        androidTestImplementation "com.google.dagger:hilt-android-testing:2.38.1"
        kaptAndroidTest "com.google.dagger:hilt-android-compiler:2.38.1"
        debugImplementation 'androidx.fragment:fragment-testing:1.4.1'
        androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'
    }
}